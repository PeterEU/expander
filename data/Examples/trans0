-- trans0

specs:		modal
constructs:	is less
defuncts:       drawI drawS

axioms:     

  states == [0,22] 
& atoms  == [is$0,is$4]++map(less)[2,4,14]

& (st < 6 ==> st -> st+1<+>ite(st`mod`2 = 0,st,()))
& 6 -> branch$[1..5]++[7..10]	  
& 7 -> 14 
& 22 -> 33<+>44

& is$st -> valid(=st)
& less$st -> valid(<st)

-- widget interpreters:
 
& drawI == wtree $ fun(x,ite(x`in`states,
			     color(index(x,states),length$states)$circ$10,
			     frame(3)$blue$text$x))

& drawS$F == wtree $ fun(x,ite(F$x,blue$frame(5)$F$text$x,text$x))

conjects:  

  (=14)$3		--> False
& AG(<14)$9		--> True
& all(EX(=4))[3,4,6]	--> True
& sat(is$4)$3		--> False
& sat(is$4)$4		--> True
& x^4^y->st		-- narrow&simplify --> 5^x^y = st | 4^x^y = st 

terms:   

    eval$is$4		--> [4]
<+> eval$less$4		--> [0,1,2,3]
<+> eval(EX$less$4)	--> [0,1,2,6]
<+> eval(EF$less$4)	--> [0..6]	
<+> eval(AF$less$4)	--> [0..3]	
<+> eval(EG$less$4)	--> [0..2]	
<+> eval(AG$less$4)	--> []
<+> eval(AG$less$14)  	--> [8..10]
<+> eval(is(4) `then` EF$less$2)	--> [0..10,14,22,33,44]
<+> eval(is(4) `then` EF$is$0)		--> [0..3,5..10,14,22,33,44]
<+> eval(AG $ is(4) `then` EF$is$0)	--> [7..10,14,22,33,44]
<+> trans(6) `meet` eval(less$2)	--> [1]
<+> f(x^4^y) 		-- rewrite&simplify --> f(5^x^y) <+> f(4^x^y)


