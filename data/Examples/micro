-- micro  (see Clarke, Grumberg, Peled, Section 4.1)

specs:      	modal
constructs: 	start close heat error
preds:		Sat 

axioms:
   
  states == [1] 
& atoms == [start,close,heat,error] 

& 1 -> branch[2,3] & 2 -> 5 & 3 -> branch[1,6] 
& 4 -> branch[1,3,4] & 5 -> branch[2,3] & 6 -> 7 & 7 -> 4

& start -> branch[2,5,6,7]
& close -> branch[3..7]
& heat -> branch[4,7]
& error -> branch[2,5]

& (Sat(at)$st <==> at `in` out$st)

conjects: 

  EF(Sat$heat)$5	   --> True (df: 89 simplifications + 2 permutations)
			   --	    (bfp: 28 simplifications + 1 permutation)	
& EG(Sat$heat)$7	   --> True (2 coinductions + 3 resolutions)
			   --	    (simplifications become cyclic)	
& AF(Sat$heat)$6	   --> True (df: 22 simplifications)
			   --       (bfp: 16 simplifications)	
& all(EF$Sat$heat)$states  --> True (72 simplifications)
& all(AF$Sat$heat)[4,6,7]  --> True (36 simplifications)

terms:

    eval(EF$heat)			--> states
<+> eval(EG$heat)			--> [4,7]
<+> stateflow(EG$heat)			--> [4,7]
<+> eval(AF$heat)			--> [4,6,7]
<+> eval(start`then`heat)		--> [1,3,4,7]
<+> eval(EX$start`then`heat)		--> [1,3,4,5,6,7]
<+> eval(error`then`AG$not$heat)	--> [1,3,4,6,7]
